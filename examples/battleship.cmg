
ensem battleship {

	module comingle.lib.ExtLib import {
		union :: ({A},{A}) -> {A}.
		size  :: {A} -> int.
		makeChain :: (A,[A]) -> ({(A,A)},A).
		mset :: [A] -> {A}.
	}

	predicate initGame :: (int,int,int,[loc]) -> trigger.
	predicate fireAt   :: (loc,int,int) -> trigger.
	predicate empty     :: (int,int) -> trigger.
	predicate hull      :: (string,int,int) -> trigger.

	predicate blastAt :: (loc,int,int) -> fact.
	predicate next      :: loc -> fact.
	predicate turn      :: fact.
	predicate all       :: {loc} -> fact.
	predicate damaged   :: (string,int,int) -> fact.
	predicate checkShip :: (loc,string) -> fact.
	predicate checkFleet :: fact.
	predicate dead       :: loc -> fact.

	predicate randomFleet  :: (int,int,int) -> actuator.
	predicate notifyTurn   :: actuator.
	predicate missed       :: (loc,loc,int,int) -> actuator.
	predicate hit          :: (loc,loc,int,int) -> actuator.
	predicate sunk         :: (loc,loc,string,{(int,int)}) -> actuator.
	predicate notifyDead   :: loc -> actuator.
	predicate notifyWinner :: loc -> actuator.

	rule start :: [I]initGame(G,F,C,Ls) 
                          --o [I]turn(), [I]notifyTurn(),
			      { [A]next(B) | (A,B) <- Cs }, [E]next(I), 
                              { [L]all(Ps), [L]randomFleet(G,F,C) | L <- Ls }
                              where (Cs,E) = makeChain(I,Ls),
                                        Ps = mset(Ls).

	rule shoot :: [A]next(B) \ [A]fireAt(D,X,Y), [A]turn()
                           --o [D]blastAt(A,X,Y), [B]turn(), [B]notifyTurn().

	rule miss :: [D]empty(X,Y) \ [D]blastAt(A,X,Y) --o [D]missed(A,D,X,Y), [A]missed(A,D,X,Y).

	rule goodHit :: [D]blastAt(A,X,Y), [D]hull(S,X,Y) --o [D]damaged(S,X,Y), [D]checkShip(A,S),
                                                              [D]hit(A,D,X,Y), [A]hit(A,D,X,Y).

	rule dmgHit  :: [D]damaged(S,X,Y) \ [D]blastAt(A,X,Y) --o [D]hit(A,D,X,Y), [A]hit(A,D,X,Y).

	rule sunk1 :: [D]all(Ps), { [D]damaged(S,X,Y)|(X,Y)<-Ds } \ [D]checkShip(A,S), 
                      { [D]hull(S,W,V)|(W,V)<-Hs } | (size(Hs))==0
                          --o { [P]sunk(A,D,S,Ds) | P <- Ps }, [D]checkFleet().

	rule sunk2 :: [D]checkShip(A,S) --o 1.

	rule deadFleet1 :: [D]all(Ps), [D]checkFleet(), { [D]hull(S,X,Y)|(S,X,Y)<-Hs } | (size(Hs))==0
                           --o { [P]notifyDead(D), [P]dead(D) | P <- Ps }.

	rule deadFleet2 :: [D]checkFleet() --o 1.

	rule winner :: [D]all(Ps), { [D]dead(O) | O->Os } | Ps==(union(Os,{D})) 
                           --o { [P]notifyWinner(D) | P<-Ps }.

}

execute battleship {

}
